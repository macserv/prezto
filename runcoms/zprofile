################################################################################
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
    export BROWSER='open'
fi

#
# Editors
#

export EDITOR='nano'
export VISUAL='nano'
export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
    export LANG='en_US.UTF-8'
fi

#
# Paths
#

# Set the list of directories that zsh searches for executables, and ensure that
# all elements are unique.
path=(
    ~/Local/{bin,sbin}
    /usr/local/{bin,sbin}    # Homebrew
    /usr/local/opt/ruby/bin  # Homebrew Ruby
    ~/.composer/vendor/bin   # Composer (PHP)
    ~/.mozbuild/git-cinnabar # mozilla
    $path
)
typeset -gU path

# Set the list of directories that `cd <path>` searches (in addition to $PWD)
# and ensure that all elements are unique.
cdpath=(
    ~
    ~/Projects/Development
    $cdpath
)
typeset -gU cdpath

# Add paths to directories containing completion data, and ensure that all
# elements are unique.
fpath=(
    /usr/local/share/zsh/site-functions  # Homebrew
    $fpath
)
typeset -gU fpath

    
#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
#export LESS='-F -g -i -M -R -S -w -X -z-4' # Original configuration
export  LESS='   -g -i -M -R -S -w    -z-4' # Scrolling enabled

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
    export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi


#
# Java
#

# Dynamically set JAVA_HOME to the currently selected JVM.
# Fail silently if it's not present.
export JAVA_HOME="$(/usr/libexec/java_home > /dev/null 2>&1; trap)"


#
# Python
#

export PYTHONSTARTUP="${HOME}/.pythonrc"



################################################################################
# Load External Profile Extension Files
# THIS SECTION SHOULD BE LAST IN THIS FILE.
#

for zprofile_file in ${ZDOTDIR:-$HOME}/.zprezto/runcoms/zprofile-*(.N); do
    source $zprofile_file
done



export PATH="$HOME/.cargo/bin:$PATH"
